/* 
 * Copyright 2016-2021 ISC Konstanz
 * 
 * This file is part of emonjava.
 * For more information visit https://github.com/isc-konstanz/emonjava
 * 
 * Emonjava is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 * 
 * Emonjava is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 * 
 * You should have received a copy of the GNU Lesser General Public License
 * along with emonjava.  If not, see <http://www.gnu.org/licenses/>.
 */
archivesBaseName = 'openmuc-datalogger-emoncms'
project.ext {
    projectFullName = 'OpenMUC Data Logger - Emoncms Java'
    projectDescription = 'Emoncms datalogger for the OpenMUC framework.'
}
configurations.create('embed')

dependencies {
    implementation fileTree(dir: 'libs/openmuc', include: ['*.jar'])
    implementation group: 'org.osgi', name: 'osgi.core', version: '7.0.0'
    implementation group: 'org.osgi', name: 'org.osgi.service.component.annotations', version: '1.4.0'
    implementation group: 'org.apache.felix', name: 'org.apache.felix.scr', version: '2.1.14'

    implementation group: 'org.ini4j', name: 'ini4j', version: '0.5.4'
    embed          group: 'org.ini4j', name: 'ini4j', version: '0.5.4'

    implementation project(':http'), project(':mqtt'), project(':sql')
    embed          project(':http'), project(':mqtt'), project(':sql')
}

def embed = configurations.embed.findAll {
    !it.getName().matches("(slf4j|protobuf|gson).*")
}

jar {
    into('libs') {
        from embed
    }
    bnd('Bundle-Name':        projectFullName,
        'Bundle-Description': projectDescription,
        'Bundle-ClassPath': '.,' + embed.collect { 'libs/'+it.getName() }.join(','),
        'Import-Package':   '!org.emoncms.*,' +
                            '!org.eclipse.paho.*,' +
                            '!redis.clients.*,' +
                            '!com.mchange.*,' +
                            '!org.ini4j.*,' +
                            '!org.jboss.*,' +
                            '!org.apache.log*,' +
                            '!com.typesafe.*,' +
                            '!net.sf.cglib*,' +
                            '!javax.swing.*,' +
                            'com.mysql.cj.jdbc,' +
                            '*;version=0',
        'Export-Package':   '')
}

eclipse {
    project {
        name = archivesBaseName
    }
}
