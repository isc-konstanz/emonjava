/* 
 * Copyright 2016-19 ISC Konstanz
 * 
 * This file is part of emonjava.
 * For more information visit https://github.com/isc-konstanz/emonjava
 * 
 * Emonjava is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 * 
 * Emonjava is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 * 
 * You should have received a copy of the GNU Lesser General Public License
 * along with emonjava.  If not, see <http://www.gnu.org/licenses/>.
 */
archivesBaseName = 'openmuc-datalogger-emoncms'
def projectFullName = 'OpenMUC Data Logger - Emoncms Java'
def projectDescription = 'Emoncms datalogger for the OpenMUC framework.'

def ini4jVersion = '0.5.4'
def ini4jJar = 'ini4j-'+ini4jVersion+'.jar'

def gsonVersion = '2.8.5'
def gsonJar = 'gson-'+gsonVersion+'.jar'

def pahoVersion = '1.2.0'
def pahoJar = 'org.eclipse.paho.client.mqttv3-'+pahoVersion+'.jar'

def apiJar = 'emoncms-api-'+projectVersion+'.jar'
def httpJar = 'emoncms-http-'+projectVersion+'.jar'
def mqttJar = 'emoncms-mqtt-'+projectVersion+'.jar'

dependencies {
  implementation fileTree(dir: 'libs/openmuc', include: ['*.jar'])
  implementation group: 'org.osgi', name: 'osgi.core', version: '7.0.0'
  implementation group: 'org.osgi', name: 'org.osgi.service.component.annotations', version: '1.4.0'
  implementation group: 'org.apache.felix', name: 'org.apache.felix.scr', version: '2.1.14'

  implementation group: 'org.ini4j', name: 'ini4j', version: ini4jVersion

  implementation project(':http'), project(':mqtt')
}

eclipse {
  project {
    name = archivesBaseName
  }
}

jar {
  into('libs') {
    from(configurations.default) {
      include apiJar, httpJar, mqttJar, ini4jJar, gsonJar, pahoJar
    }
  }
  manifest {
    attributes('Bundle-Name':        projectFullName,
               'Bundle-Description': projectDescription,
               'Bundle-ClassPath':   '.,libs/'+apiJar+',' +
                                       'libs/'+httpJar+',' +
                                       'libs/'+mqttJar+',' +
                                       'libs/'+ini4jJar+',' +
                                       'libs/'+gsonJar+',' +
                                       'libs/'+pahoJar,
               'Import-Package':      '!org.emoncms.*,' +
                                      '!org.ini4j.*,' +
                                      '!com.google.gson.*,' +
                                      '!org.eclipse.paho.*,' +
                                       'org.slf4j;version=0,' +
                                       'org.osgi.*;version=0,' +
                                       'org.openmuc.*;version=0',
               'Export-Package':     '')
  }
}
