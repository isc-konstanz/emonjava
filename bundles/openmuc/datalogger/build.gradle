/* 
 * Copyright 2016-19 ISC Konstanz
 * 
 * This file is part of emonjava.
 * For more information visit https://github.com/isc-konstanz/emonjava
 * 
 * Emonjava is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 * 
 * Emonjava is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 * 
 * You should have received a copy of the GNU Lesser General Public License
 * along with emonjava.  If not, see <http://www.gnu.org/licenses/>.
 */
archivesBaseName = 'openmuc-datalogger-emoncms'
def projectFullName = 'OpenMUC Data Logger - Emoncms Java'
def projectDescription = 'Emoncms datalogger for the OpenMUC framework.'

def ini4jVersion = '0.5.4'
def gsonVersion = '2.8.5'
def pahoVersion = '1.2.0'

configurations.create('embed')

dependencies {
  implementation fileTree(dir: 'libs/openmuc', include: ['*.jar'])

  implementation group: 'org.osgi', name: 'org.osgi.core', version: '6.0.0'
  implementation group: 'org.osgi', name: 'org.osgi.service.component.annotations', version: '1.3.0'
  implementation group: 'org.apache.felix', name: 'org.apache.felix.scr', version: '2.0.10'
  implementation group: 'org.apache.felix', name: 'org.apache.felix.http.api', version: '3.0.0'

  implementation group: 'org.ini4j', name: 'ini4j', version: ini4jVersion

  implementation project(':http'), project(':mqtt')

  embed project(':api'), project(':http'), project(':mqtt')

  embed group: 'org.ini4j', name: 'ini4j', version: ini4jVersion
  embed group: 'com.google.code.gson', name: 'gson', version: gsonVersion
  embed group: 'org.eclipse.paho', name: 'org.eclipse.paho.client.mqttv3', version: pahoVersion
}

eclipse {
  project {
    name = archivesBaseName
  }
}

jar {
  into('libs') {
    exclude 'slf4j*.jar'
    from configurations.embed
  }
  manifest {
    attributes('Bundle-Name':        projectFullName,
               'Bundle-Description': projectDescription,
               'Bundle-ClassPath':   '.,libs/emoncms-api-'+projectVersion+'.jar,' +
                                       'libs/emoncms-http-'+projectVersion+'.jar,' +
                                       'libs/emoncms-mqtt-'+projectVersion+'.jar,' +
                                       'libs/ini4j-'+ini4jVersion+'.jar,' +
                                       'libs/gson-'+gsonVersion+'.jar,' +
                                       'libs/org.eclipse.paho.client.mqttv3-'+pahoVersion+'.jar',
               'Import-Package':     '!org.emoncms.*,!com.google.gson.*,!org.eclipse.paho.*,!org.ini4j.*,org.slf4j,*;version=0',
	           'Export-Package':     '')
  }
}
